#!-------------------------------------------------------------------------------------------------!
#!  DFTB+: general package for performing fast atomistic simulations                               !
#!  Copyright (C) 2006 - 2021  DFTB+ developers group                                              !
#!                                                                                                 !
#!  See the LICENSE file for terms of usage and distribution.                                      !
#!-------------------------------------------------------------------------------------------------!

#:mute
#:if not defined('_ERROR_FYPP_')
#:set _ERROR_FYPP_

#:include 'common.fypp'


#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#! Error string handling wrappers for returns from routines (with integer as error flag)
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


#! Macro to return an error flag if return variable available or throw
#! an error and shut down otherwise
#:def ERROR_HANDLING(errVar, errNumber, msg)
  block
    use dftbp_common_accuracy, only : lc
    use dftbp_io_message
    !> Error handling string
    character(lc) :: stringTmp

    write(stringTmp,"(A)")${msg}$
    if (present(${errVar}$)) then
      ${errVar}$ = ${errNumber}$
      call warning(stringTmp)
      return
    else
      call error(stringTmp)
    end if
  end block
#:enddef ERROR_HANDLING


#! Macro to return an error flag if return variable available or throw
#! an error and shut down otherwise
#:def FORMATTED_ERROR_HANDLING(errVar, errNumber, formating, *variables)
  block
    use dftbp_common_accuracy, only : lc
    use dftbp_io_message
    !> Error handling string
    character(lc) :: stringTmp

    write(stringTmp,${formating}$) ${ ",".join(variables) }$
    if (present(${errVar}$)) then
      ${errVar}$ = ${errNumber}$
      call warning(stringTmp)
      return
    else
      call error(stringTmp)
    end if
  end block
#:enddef FORMATTED_ERROR_HANDLING


#! Propagation of error handling, for now it just returns when in error
#:def HANDLE_ERROR(errVar)
  if (present(${errVar}$)) then
    if (${errVar}$ /= 0) then
      return
    end if
  end if
#:enddef


#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#! Exception like error handling
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


#! Raises (creates and propagates) an error
#:def RAISE_ERROR(error, code, message)
  block
    use dftbp_common_error, only : TError_create
    call TError_create(${error}$, ${code}$, ${message}$)
    #:block DEBUG_CODE
      call ${error}$%addPropagationInfo("${_FILE_}$", ${_LINE_}$)
    #:endblock
    return
  end block
#:enddef


#! Raises (creates and propagates) an error with a formatted error message
#:def RAISE_FORMATTED_ERROR(error, code, format, *messageVars)
  block
    character(1024) :: message
    write(message, ${format}$) ${",".join(messageVars)}$
    @:RAISE_ERROR(${error}$, ${code}$, trim(message))
  end block
#:enddef


#! Deactivates an error.
#:def DEACTIVATE_ERROR(error)
  call ${error}$%deactivate()
#:enddef


#! Propagates an error upwards
#:def PROPAGATE_ERROR(error)
  if (allocated(${error}$)) then
    #:block DEBUG_CODE
      call ${error}$%addPropagationInfo("${_FILE_}$", ${_LINE_}$)
    #:endblock
    return
  end if
#:enddef


#! Deactivates error, executes error handling code and deallocates error
#:def CATCH_ERROR(error, ERROR_HANDLER)
  if (allocated(${error}$)) then
    @:DEACTIVATE_ERROR(${error}$)
    block
      $:ERROR_HANDLER
    end block
    deallocate(${error}$)
  end if
#:enddef


#:endif
#:endmute
