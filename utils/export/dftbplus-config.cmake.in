@PACKAGE_INIT@

# Global DFTB+ config options
set(DftbPlus_WITH_OMP @WITH_OMP@)
set(DftbPlus_WITH_MPI @WITH_MPI@)
set(DftbPlus_WITH_ELSI @WITH_ELSI@)
set(DftbPlus_ELSI_WITH_PEXSI @ELSI_WITH_PEXSI@)
set(DftbPlus_WITH_GPU @WITH_GPU@)
set(DftbPlus_WITH_TRANSPORT @WITH_TRANSPORT@)
set(DftbPlus_WITH_SOCKETS @WITH_SOCKETS@)
set(DftbPlus_WITH_ARPACK @WITH_ARPACK@)
set(DftbPlus_WITH_MBD @WITH_MBD@)
set(DftbPlus_WITH_PLUMED @WITH_PLUMED@)
set(DftbPlus_WITH_SDFTD3 @WITH_SDFTD3@)
set(DftbPlus_WITH_TBLITE @WITH_TBLITE@)
set(DftbPlus_WITH_CHIMES @WITH_CHIMES@)

include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/Modules)

if(NOT TARGET DftbPlus::DftbPlus)

  if(DftbPlus_WITH_OMP AND NOT TARGET OpenMP::OpenMP_Fortran)
    find_dependency(OpenMP)
  else()
    # DFTB+ uses dummy target for non-OMP compilation
    add_library(OpenMP::OpenMP_Fortran INTERFACE IMPORTED)
  endif()

  if(NOT TARGET BLAS::BLAS)
    find_dependency(CustomBlas)
  endif()

  if(NOT TARGET LAPACK::LAPACK)
    find_dependency(CustomLapack)
  endif()

  if(DftbPlus_WITH_MPI)
    if(NOT TARGET ScalapackFx::ScalapackFx)
      find_dependency(ScalapackFx)
    endif()
    if(NOT TARGET MpiFx::MpiFx)
      find_dependency(MpiFx)
    endif()
  endif()

  if(DftbPlus_WITH_ELSI AND NOT TARGET elsi::elsi)
    find_dependency(elsi)
  endif()

  if(DftbPlus_WITH_GPU AND NOT TARGET Magma::Magma)
    find_dependency(CustomMagma)
  endif()

  if(DftbPlus_WITH_ARPACK AND NOT TARGET ARPACK::ARPACK)
    find_dependency(arpackng)
  endif()

  if(DftbPlus_WITH_PLUMED AND NOT TARGET Plumed::Plumed)
    find_dependency(CustomPlumed)
  endif()

  if(DftbPlus_WITH_MBD AND NOT TARGET Mbd::Mbd)
    find_dependency(Mbd)
  endif()

  if(DftbPlus_WITH_TRANSPORT AND NOT TARGET Negf::Negf)
    find_dependency(Negf)
  endif()

  if(DftbPlus_WITH_SDFTD3 AND NOT TARGET s-dftd3::s-dftd3)
    find_dependency(s-dftd3)
  endif()

  if(DftbPlus_WITH_TBLITE AND NOT TARGET tblite::tblite)
    find_dependency(tblite)
  endif()

  if(DftbPlus_WITH_CHIMES AND NOT TARGET ChimesCalc::ChimesCalc_Fortran)
    find_dependency(ChimesCalc)
  endif()

  include("${CMAKE_CURRENT_LIST_DIR}/dftbplus-targets.cmake")

endif()
